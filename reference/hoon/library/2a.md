##chapter 2a, basic unsigned math

###++add 

```
++  add                                                 ::  add
  ~/  %add
  |=  [a=@ b=@]
  ^-  @
  ?:  =(0 a)  b
  $(a (dec a), b +(b))
::
```
        
Sum two numbers.

####Examples

        ~palryp-hocsyt/try=> (add 2 2)
        4
        ~palryp-hocsyt/try=> (add 1 1.000.000)
        1.000.001
        ~palryp-hocsyt/try=> (add 1.333 (mul 2 2))
        1.337

###++cap

```
++  cap                                                 ::  tree head
  ~/  %cap
  |=  a=@
  ^-  ?(%2 %3)
  ?-  a
    %2        %2
    %3        %3
    ?(%0 %1)  !!
    *         $(a (div a 2))
  ==
::
```
          
Test if an atom is in the head or tail of a noun.

####Examples

        ~palryp-hocsyt/try=> (cap 4)
        %2
        ~palryp-hocsyt/try=> (cap 6)
        %3
        ~palryp-hocsyt/try=> (cap (add 10 9))
        %2

###++dec  

```
++  dec                                                 ::  decrement
  ~/  %dec
  |=  a=@
  ~|  %decrement-underflow
  ?<  =(0 a)
  =+  b=0
  |-  ^-  @
  ?:  =(a +(b))  b
  $(b +(b))
::
```

Decrement a number - Subtracts one.

####Examples

        ~palryp-hocsyt/try=> (dec 7)
        6
        ~palryp-hocsyt/try=> (dec 0)
        ! decrement-underflow
        ! exit

###++div  

```
++  div                                                 ::  divide
  ~/  %div
  |=  [a=@ b=@]
  ^-  @
  ~|  'div'
  ?<  =(0 b)
  =+  c=0
  |-
  ?:  (lth a b)  c
  $(a (sub a b), c +(c))
::
```

Divide one number by another.

####Examples

        ~palryp-hocsyt/try=> (div 4 2)
        2
        ~palryp-hocsyt/try=> (div 17 8)
        2
        ~palryp-hocsyt/try=> (div 20 30)
        0

###++fac

```
++  fac                                                 ::  factorial
  ~/  %fac
  |=  a=@
  ^-  @
  ?:  =(0 a)  1
  (mul a $(a (dec a)))
::
```
  
Produce the factorial of a number n, n!.

####Examples

        ~palryp-hocsyt/try=> (fac 3)
        6
        ~palryp-hocsyt/try=> (fac 0)
        1
        ~palryp-hocsyt/try=> (fac 11)
        39.916.800

###++gte

```
++  gte                                                 ::  greater-equal
  ~/  %gte
  |=  [a=@ b=@]
  ^-  ?
  !(lth a b)
::
```

Is the first greater than or equal to the second?

####Examples

        ~palryp-hocsyt/try=> (gte 100 10)
        %.y
        ~palryp-hocsyt/try=> (gte 4 4)
        %.y
        ~palryp-hocsyt/try=> (gte 3 4)
        %.n

###++gth

```
++  gth                                                 ::  greater-than
  ~/  %gth
  |=  [a=@ b=@]
  ^-  ?
  !(lte a b)
::
```

Is the first greater than the second?

###++lte

```
++  lte                                                 ::  less-equal
  ~/  %lte
  |=  [a=@ b=@]
  |(=(a b) (lth a b))
::
```
  
Is the first less than or equal to the second?

          ~ronrem-lonsem/try=> (lte 4 5)
          %.y
          ~ronrem-lonsem/try=> (lte 5 4)
          %.n
          ~ronrem-lonsem/try=> (lte 5 5)
          %.y
          ~ronrem-lonsem/try=> (lte 0 0)
          %.y

###++lth

```
++  lth                                                 ::  less-than
  ~/  %lth
  |=  [a=@ b=@]
  ^-  ?
  ?&  !=(a b) 
      |-  
      ?|  =(0 a)  
          ?&  !=(0 b) 
              $(a (dec a), b (dec b))
  ==  ==  ==
::
```
  
Is the first less than the second?

####Examples          

          ~ronrem-lonsem/try=> (lth 4 5)
          %.y
          ~ronrem-lonsem/try=> (lth 5 4)
          %.n
          ~ronrem-lonsem/try=> (lth 5 5)
          %.n
          ~ronrem-lonsem/try=> (lth 5 0)
          %.n

###++mas

```
++  mas                                                 ::  tree body
  ~/  %mas
  |=  a=@
  ^-  @
  ?-  a
    1   !!
    2   1
    3   1
    *   (add (mod a 2) (mul $(a (div a 2)) 2))
  ==
::
```
  
Produce the axis of a within the head or the tail. 

####Examples
 
          1 ~ronrem-lonsem/try=> (mas 3)
          1
          ~ronrem-lonsem/try=> (mas 4)
          2
          ~ronrem-lonsem/try=> (mas 5)
          3
          ~ronrem-lonsem/try=> (mas 6)
          2
          ~ronrem-lonsem/try=> (mas 7)
          3
          ~ronrem-lonsem/try=> (mas 8)
          4
          ~ronrem-lonsem/try=> (mas 0)
          ! exit
          ~ronrem-lonsem/try=> (mas 1)
          ! exit

###++max

```
++  max                                                 ::  maximum
  ~/  %max
  |=  [a=@ b=@]
  ^-  @
  ?:  (gth a b)  a
  b
::
```
  
Produce the larger of two atoms.

####Examples

          ~palryp-hocsyt/try=> (max 10 100)
          100
          ~palryp-hocsyt/try=> (max 10.443 9)
          10.443
          ~palryp-hocsyt/try=> (max 0 1)
          1

###++min  

```
++  min                                                 ::  minimum
  ~/  %min
  |=  [a=@ b=@]
  ^-  @
  ?:  (lth a b)  a
  b
::
```

Produce the smaller of two atoms.

####Examples

        ~palryp-hocsyt/try=> (min 10 100)
        10
        ~palryp-hocsyt/try=> (min 10.443 9)
        9
        ~palryp-hocsyt/try=> (min 0 1)
        0

###++mod  

```
++  mod                                                 ::  remainder
  ~/  %mod
  |=  [a=@ b=@]
  ^-  @
  ?<  =(0 b)
  (sub a (mul b (div a b)))
::
```


###++mul 

```
++  mul                                                 ::  multiply
  ~/  %mul
  |=  [a=@ b=@]
  ^-  @
  =+  c=0
  |-
  ?:  =(0 a)  c
  $(a (dec a), c (add b c))
::
```
 
Multiply two numbers

####Examples

        ~sivtyv-barnel/try=> (mul 3 4)
         12 
        ~sivtyv-barnel/try=> (mul 0 1) 
        0

###++peg  

```
++  peg                                                 ::  tree connect
  ~/  %peg
  |=  [a=@ b=@]
  ^-  @
  ?-  b
    1   a
    2   (mul a 2)
    3   +((mul a 2))
    *   (add (mod b 2) (mul $(b (div b 2)) 2))
  ==
::
```

Produces the axis of b within the axis of a.

####Examples

        ~ronrem-lonsem/try=> (mul 5 3)
        15
        ~ronrem-lonsem/try=> (mul 1 0)
        0

###++sub  

```
++  sub                                                 ::  subtract
  ~/  %sub
  |=  [a=@ b=@]
  ~|  %subtract-underflow
  ^-  @
  ?:  =(0 b)  a
  $(a (dec a), b (dec b))
```

Subtract two numbers

####Examples

        ~ronrem-lonsem/try=> (sub 10 5)
        5
        ~ronrem-lonsem/try=> (sub 243 44)
        199
        ~ronrem-lonsem/try=> (sub 5 0)
        5
        ~ronrem-lonsem/try=> (sub 0 5)
        ! subtract-underflow
        ! exit


