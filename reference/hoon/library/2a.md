##chapter 2a, basic unsigned math

###++add 

```
++  add                                                 ::  add
  ~/  %add
  |=  [a=@ b=@]
  ^-  @
  ?:  =(0 a)  b
  $(a (dec a), b +(b))
::
```
        
Sum two numbers.

    ~zod/try=> (add 2 2)
    4
    ~zod/try=> (add 1 1.000.000)
    1.000.001
    ~zod/try=> (add 1.333 (mul 2 2))
    1.337

###++cap

```
++  cap                                                 ::  tree head
  ~/  %cap
  |=  a=@
  ^-  ?(%2 %3)
  ?-  a
    %2        %2
    %3        %3
    ?(%0 %1)  !!
    *         $(a (div a 2))
  ==
::
```
          
Test if an atom is in the head or tail of a noun.

    ~zod/try=> (cap 4)
    %2
    ~zod/try=> (cap 6)
    %3
    ~zod/try=> (cap (add 10 9))
    %2

###++dec  

```
++  dec                                                 ::  decrement
  ~/  %dec
  |=  a=@
  ~|  %decrement-underflow
  ?<  =(0 a)
  =+  b=0
  |-  ^-  @
  ?:  =(a +(b))  b
  $(b +(b))
::
```

Decrement a number - Subtracts one.

    ~zod/try=> (dec 7)
    6
    ~zod/try=> (dec 0)
    ! decrement-underflow
    ! exit

###++div  

```
++  div                                                 ::  divide
  ~/  %div
  |=  [a=@ b=@]
  ^-  @
  ~|  'div'
  ?<  =(0 b)
  =+  c=0
  |-
  ?:  (lth a b)  c
  $(a (sub a b), c +(c))
::
```

Divide one number by another.

    ~zod/try=> (div 4 2)
    2
    ~zod/try=> (div 17 8)
    2
    ~zod/try=> (div 20 30)
    0

###++fac

```
++  fac                                                 ::  factorial
  ~/  %fac
  |=  a=@
  ^-  @
  ?:  =(0 a)  1
  (mul a $(a (dec a)))
::
```
  
Produce the factorial of a number n, n!.

    ~zod/try=> (fac 3)
    6
    ~zod/try=> (fac 0)
    1
    ~zod/try=> (fac 11)
    39.916.800

###++gte

```
++  gte                                                 ::  greater-equal
  ~/  %gte
  |=  [a=@ b=@]
  ^-  ?
  !(lth a b)
::
```

Is the first greater than or equal to the second?

    ~zod/try=> (gte 100 10)
    %.y
    ~zod/try=> (gte 4 4)
    %.y
    ~zod/try=> (gte 3 4)
    %.n

###++gth

```
++  gth                                                 ::  greater-than
  ~/  %gth
  |=  [a=@ b=@]
  ^-  ?
  !(lte a b)
::
```

Is the first greater than the second?

    ~zod/try=> (gth 'd' 'c')
    %.y
    ~zod/try=> (gth ~h1 ~m61)
    %.n


###++lte

```
++  lte                                                 ::  less-equal
  ~/  %lte
  |=  [a=@ b=@]
  |(=(a b) (lth a b))
::
```
  
Is the first less than or equal to the second?

      ~zod/try=> (lte 4 5)
      %.y
      ~zod/try=> (lte 5 4)
      %.n
      ~zod/try=> (lte 5 5)
      %.y
      ~zod/try=> (lte 0 0)
      %.y

###++lth

```
++  lth                                                 ::  less-than
  ~/  %lth
  |=  [a=@ b=@]
  ^-  ?
  ?&  !=(a b) 
      |-  
      ?|  =(0 a)  
          ?&  !=(0 b) 
              $(a (dec a), b (dec b))
  ==  ==  ==
::
```
  
Is the first less than the second?

      ~zod/try=> (lth 4 5)
      %.y
      ~zod/try=> (lth 5 4)
      %.n
      ~zod/try=> (lth 5 5)
      %.n
      ~zod/try=> (lth 5 0)
      %.n

###++mas

```
++  mas                                                 ::  tree body
  ~/  %mas
  |=  a=@
  ^-  @
  ?-  a
    1   !!
    2   1
    3   1
    *   (add (mod a 2) (mul $(a (div a 2)) 2))
  ==
::
```
  
Produce the axis of a within the head or the tail. 

      ~zod/try=> (mas 3)
      1
      ~zod/try=> (mas 4)
      2
      ~zod/try=> (mas 5)
      3
      ~zod/try=> (mas 6)
      2
      ~zod/try=> (mas 7)
      3
      ~zod/try=> (mas 8)
      4
      ~zod/try=> (mas 0)
      ! exit
      ~zod/try=> (mas 1)
      ! exit

###++max

```
++  max                                                 ::  maximum
  ~/  %max
  |=  [a=@ b=@]
  ^-  @
  ?:  (gth a b)  a
  b
::
```
  
Produce the larger of two atoms.

          ~zod/try=> (max 10 100)
          100
          ~zod/try=> (max 10.443 9)
          10.443
          ~zod/try=> (max 0 1)
          1

###++min  

```
++  min                                                 ::  minimum
  ~/  %min
  |=  [a=@ b=@]
  ^-  @
  ?:  (lth a b)  a
  b
::
```

Produce the smaller of two atoms.

        ~zod/try=> (min 10 100)
        10
        ~zod/try=> (min 10.443 9)
        9
        ~zod/try=> (min 0 1)
        0

###++mod  

```
++  mod                                                 ::  remainder
  ~/  %mod
  |=  [a=@ b=@]
  ^-  @
  ?<  =(0 b)
  (sub a (mul b (div a b)))
::
```


###++mul 

```
++  mul                                                 ::  multiply
  ~/  %mul
  |=  [a=@ b=@]
  ^-  @
  =+  c=0
  |-
  ?:  =(0 a)  c
  $(a (dec a), c (add b c))
::
```
 
Multiply two numbers

        ~zod/try=> (mul 3 4)
         12 
        ~zod/try=> (mul 0 1) 
        0

###++peg  

```
++  peg                                                 ::  tree connect
  ~/  %peg
  |=  [a=@ b=@]
  ^-  @
  ?-  b
    1   a
    2   (mul a 2)
    3   +((mul a 2))
    *   (add (mod b 2) (mul $(b (div b 2)) 2))
  ==
::
```

Produces the axis of b within the axis of a.

        ~zod/try=> (mul 5 3)
        15
        ~zod/try=> (mul 1 0)
        0

###++sub  

```
++  sub                                                 ::  subtract
  ~/  %sub
  |=  [a=@ b=@]
  ~|  %subtract-underflow
  ^-  @
  ?:  =(0 b)  a
  $(a (dec a), b (dec b))
```

Subtract two numbers

        ~zod/try=> (sub 10 5)
        5
        ~zod/try=> (sub 243 44)
        199
        ~zod/try=> (sub 5 0)
        5
        ~zod/try=> (sub 0 5)
        ! subtract-underflow
        ! exit
